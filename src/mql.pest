// reminder on pest grammar:
// _{ } => silent: rule won't appear in a parse result
// ${ } => compound atomic: no whitespace, inner tokens as normal
// @{ } => atomic: no whitespace, no inner tokens

// In general, when writing a parser with choices, put the longest or
// most specific choice first, and the shortest or most general choice
// last.

WHITESPACE = _{ " " }

alpha = _{ LETTER }
digit = { '0'..'9' }

prefix = { ("@"|".") }

// a key may contain special characters, but it must not start with one
// key key_char should be kept in sync with memo.pest
key_char = { alpha | digit | "_" | ":" | "/" | "."  | "-" }
key = @{ alpha ~ key_char* }

// unquoted value
unquoted_value = @{ (!(" " | ",") ~ ANY)* }

// quoted value
inner_value = @{ (!POP ~ ANY)* }
quote_char = _{ "'" | "\"" }
quoted_value = _{ PUSH(quote_char) ~ inner_value ~ POP}

// value can either be quoted or unquoted
value = { quoted_value | unquoted_value }

operator = { "=" | "~" | "<=" | "<" | ">=" | ">" }

index = { digit+ }
index_from = { index }
index_to = { index }
index_single = { "[" ~ index ~ "]" }
index_range = { "[" ~ index_from ~ ":" ~ index_to ~ "]" }
index_expr = _{ index_range | index_single }

c1 = _{ prefix? ~ key? ~ (operator ~ value)? }
c2 = _{ prefix? ~ key? ~ index_expr ~ (operator ~ value)? }
condition = ${ c2 | c1 }


mql = _{ SOI ~ condition ~ ("," ~ condition)* ~ EOI }

WHITESPACE = _{ " " }

alpha = _{ LETTER }
digit = { '0'..'9' }

// key key_char should be kept in sync with mql.pest
key_char = { alpha | digit | "_" | ":" | "/" | "."  | "-" }
key = @{ alpha ~ key_char* }

value_char = { !NEWLINE ~ ANY }
value = @{ value_char* }

multiline_value = { (!POP ~ value ~ NEWLINE)* }

eof = @{ value_char+ }
sep = @{ "," | "|" | ";" }

comment = { "#" ~ value }

header = { "@" ~ key ~ value }
multivalue_node = { "." ~ key ~ sep ~ value }
node = { "." ~ key ~ value }
attr = { "+" ~ key ~ value }
multiline_node = {
      "." ~ key ~ "<<" ~ PUSH(eof) ~ NEWLINE ~
      multiline_value ~
      POP
}

// data and header node, implicit multiline
prefix_char = @{ "." | "@" | "+" } // TODO: attributes 
value_ml = @{ value? ~ (NEWLINE ~ !prefix_char ~ value)* }
data_node_ml = { "." ~ key ~ value_ml ~ (NEWLINE ~ attr)* }
data_multinode_ml = { "." ~ key ~ sep ~ value_ml }
header_node_ml = { "@" ~ key ~ value_ml }

// data and header, explicit multiline, i.e. with separator
value_eof = { (!POP ~ value ~ NEWLINE)* }
header_node_eof = { "@" ~ key ~ "<<" ~ PUSH(eof) ~ NEWLINE ~ value_eof ~ POP }
data_node_eof = { "." ~ key ~ "<<" ~ PUSH(eof) ~ NEWLINE ~ value_eof ~ POP }
data_multinode_eof = { "." ~ key ~ sep ~ "<<" ~ PUSH(eof) ~ NEWLINE ~ value_eof ~ POP }

header_node = { header_node_eof | header_node_ml }
data_node = { data_node_eof | data_node_ml }
data_multinode = { data_multinode_eof | data_multinode_ml }
memo = { header_node ~ (NEWLINE ~ (data_multinode|data_node))* }

memos = { (comment | memo | NEWLINE)* }

file = {
      SOI ~
      ((comment | header | multiline_node | multivalue_node | node | attr )? ~ NEWLINE)* ~
    EOI
}


